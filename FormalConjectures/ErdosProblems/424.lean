/-
Copyright 2025 The Formal Conjectures Authors.

Licensed under the Apache License, Version 2.0 (the "License");
you may not use this file except in compliance with the License.
You may obtain a copy of the License at

    https://www.apache.org/licenses/LICENSE-2.0

Unless required by applicable law or agreed to in writing, software
distributed under the License is distributed on an "AS IS" BASIS,
WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
See the License for the specific language governing permissions and
limitations under the License.
-/
import FormalConjectures.Util.ProblemImports

/-!
# Erdős Problem 424: Sequence generated by $a_i a_j - 1$

*References:*
 - [erdosproblems.com/424](https://www.erdosproblems.com/424)
 - [A005244](https://oeis.org/A005244)
 - [Ben Green's Open Problem 63](https://people.maths.ox.ac.uk/greenbj/papers/open-problems.pdf#section.8 Problem 63)
-/

namespace Erdos424

open Set

/--
Defines the set of new numbers generated from a set A by the operation
$x y - 1$ for $x \neq y$.
-/
def nextGeneration (A : Set ℕ) : Set ℕ :=
  { z : ℕ | ∃ x y, x ∈ A ∧ y ∈ A ∧ x ≠ y ∧ z = x * y - 1 }

/--
The sequence of sets $A_n$ where $A_0 = \{2, 3\}$ and $A_{n+1}$ is $A_n$ union all newly
generated elements.
-/
def sequenceSet : ℕ → Set ℕ
  | 0 => {2, 3}
  | n + 1 => (sequenceSet n) ∪ (nextGeneration (sequenceSet n))

/-- The set of integers which eventually appear in the sequence, which is the union of all $A_n$. -/
def generatedSet : Set ℕ := ⋃ n : ℕ, sequenceSet n

/--
Let $a_1=2$ and $a_2=3$ and continue the sequence by appending to $a_1,\ldots,a_n$ all possible
values of $a_ia_j-1$ with $i\neq j$.
Is it true that the set of integers which eventually appear has positive density?
-/
@[category research open, AMS 11]
theorem erdos_424 : generatedSet.HasPosDensity ↔ answer(sorry) := by
  sorry

-- TODO(firsching): formalize the statements from the additional material

end Erdos424
